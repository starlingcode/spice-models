*** Voltage regulators (negative)

.SUBCKT x_LM79XX Input Output Ground PARAMS:
+ Av_feedback=1660, R1_Value=4615,
+ Rbg_Tc1=8.13E-5, Rbg_Tc2=0.0,
+ Rout_Value=0.01, Rreg_Value=1.2k
*
* SERIES 3-TERMINAL NEGATIVE REGULATOR
*
* Note: This regulator is based on the LM79XX series of
* regulators (also the LM120 and LM320). The
* LM79XX regulators are unstable and will
* oscillate unless a 1 uFarad solid tantalum
* capacitor is placed on the output with an ESR
* betweed .5 and 1.5. This model is stable without
* a capacitor on the output. When performing
* simulations a 1 uFarad capacitor should still be
* placed on the output. However, it it not necessary
* to include a resistor in series with this capacitor
* to model the ESR of the capacitor. See the
* comments and circuit description of the x_LM78XX
* regulator for more information on this model.
*
* Band-gap voltage source:
*
Vbg 100 0 DC -7.4V
Sbg (100,101),(Ground,Input) Sbg1
Rbg 101 0 Rbg1 1
.MODEL Rbg1 RES (Tc1={Rbg_Tc1},Tc2={Rbg_Tc2})
Ebg (102,0),(Input,Ground) 1
Rreg 102 101 {Rreg_Value}
.MODEL Sbg1 VSWITCH (Ron=1 Roff=1MEG Von=3.7 Voff=3)
*
* Feedback stage
*
Rfb 9 8 1MEG
Cfb 8 Ground 265PF
* Eopamp 105 0 VALUE={2250*v(101,0)+Av_feedback*v(Ground,8)}
Vgainf 200 0 {Av_feedback}
Rgainf 200 0 1
Eopamp 105 0 POLY(3),(101,0),(Ground,8),(200,0) 0 2250 0 0 0 0 0 0 1
Ro 105 106 1k
D1 108 106 Dlim
D2 106 107 Dlim
.MODEL Dlim D (Vj=0.7)
Vl1 108 102 DC 1
Vl2 0 107 DC 1
*
* Quiescent current modelling
*
Gq (Ground,Input),(9,Input) 9.0E-7
R1 9 Ground {R1_Value} TC=0.001
Fl (Ground,0) Vmon 3.0E-4
*
* Output Stage
*
Q1 9 5 6 Npn1
Q2 9 6 7 Npn1 10
.MODEL Npn1 NPN (Bf=50 Is=1E-14)
* Efb 4 Ground VALUE={v(Input,Ground)+v(0,106)}
Efb 4 Ground POLY(2),(Input,Ground),(0,106) 0 1 1
Rb 4 5 1k TC=0.003
Re 6 7 2k
Rsc 7 Input 0.13 TC=1.136E-3,-7.806E-6
Rout 9 Imon {Rout_Value}
Vmon Imon Output DC 0.0
*
* Current Limit
*
Qcl1 54 52 53 Npn1
Qcl3 Input 54 5 Pnp1
.MODEL Pnp1 PNP (Bf=250 Is=1E-14)
Rcl3 5 54 1.8k
Qcl2 52 52 51 Npn1
Veset 53 Input DC 0.3v
Ibias Input 52 DC 300u
Rcl1 50 51 20k
Rcl2 51 7 115
Dz1 50 9 Dz
.MODEL Dz D (Is=0.05p Rs=3 Bv=7.11 Ibv=0.05u)
.ENDS
*$
*
*---------------------------------------------------------------LM7905C
.SUBCKT LM7905C Input Output Ground
x1 Input Output Ground x_LM79XX PARAMS:
+ Av_feedback=1660, R1_Value=4615,
+ Rbg_Tc1=8.13E-5, Rbg_Tc2=0.0,
+ Rout_Value=0.01, Rreg_Value=1.2k
.ENDS
*$
*